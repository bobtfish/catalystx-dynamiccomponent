package CatalystX::DynamicComponent::ModelToControllerReflector;
use Moose::Role;
use Moose::Util qw/does_role/;
use List::MoreUtils qw/uniq/;
use Moose::Autobox;
use namespace::autoclean;

my $mangle_attributes_on_generated_methods = sub {
    my ($meta, $config) = @_;
    foreach my $name (keys %{ $config->{methods}}) {
        my $m = $meta->get_method($name);
        $meta->register_method_attributes($m->body, ['Local']);
    }
};

with 'CatalystX::DynamicComponent' => {
    name => '_setup_dynamic_controller',
    pre_immutable_hook => $mangle_attributes_on_generated_methods,
};

requires 'setup_components';

after 'setup_components' => sub { shift->_setup_dynamic_controllers(@_); };

sub _setup_dynamic_controllers {
    my ($app) = @_;

    my @model_names = grep { /::Model::/ } keys %{ $app->components };
    foreach my $model_name (@model_names) {
        $app->_reflect_model_to_controller( $model_name, $app->components->{$model_name} );
    }
}

sub _reflect_model_to_controller {
    my ( $app, $model_name, $model ) = @_;

    # Model passed in as MyApp::Model::Foo, strip MyApp
    $model_name =~ s/^[^:]+:://;

    # Get Controller::Foo
    my $controller_name = $model_name;
    $controller_name =~ s/^Model::/Controller::/;

    # Get Foo
    my $suffix = $model_name;
    $suffix =~ s/Model:://;

    my %controller_methods;
    # FIXME - Abstract this strategy crap out.
    my $model_methods = $model->meta->get_method_map;
    my $interface_roles = [ uniq(($app->config->{$model_name}->{interface_roles}||=[])->flatten,
        ($app->config->{'CatalystX::DynamicComponent::ModelToControllerReflector'}->{interface_roles}||=[])->flatten) ];
    
    for my $interface_role (@$interface_roles) {
            for my $required_method ($interface_role->meta->get_required_method_list) {
                    # Note need to pass model name, as the method actually comes from
                    # the underlying model class, not the Catalyst shim class we autogenerated.
                    $controller_methods{$required_method} = 
                         $app->generate_reflected_controller_action_method($suffix, $model_methods->{$required_method})
            }
    }

    # Shallow copy so we don't stuff method refs in config
    my $config = { %{$app->config->{$controller_name}||{}} };

    $config->{methods} = \%controller_methods;
    $app->_setup_dynamic_controller( $controller_name, $config );
}

sub generate_reflected_controller_action_method {
    my ( $app, $model, $method ) = @_;
    my $method_name = $method->name; # Is it worth passing the actual method object here?
    sub {
        my ($self, $c, @args) = @_;
        $c->res->header('X-From-Model', $model);
        my $response = $c->model($model)->$method_name($c->req->data);
        $c->res->header('X-From-Model-Data', $response);
        $c->res->body('OK');
        $c->stash->{response} = $response;
    };
}

1;

__END__

=head1 NAME

CatalystX::DynamicComponent::ModelToControllerReflector - Generate Catalyst controllers automaticall from models and configuration.

=head1 SYNOPSIS

=head1 DESCRIPTION

=head1 LINKS

L<Catalyst>, L<MooseX::MethodAttributes>, L<CatalystX::ModelsFromConfig>.

=head1 BUGS

Probably plenty, test suite certainly isn't comprehensive.. Patches welcome.

=head1 AUTHOR

Tomas Doran (t0m) <bobtfish@bobtfish.net>

=head1 LICENSE

This code is copyright (c) 2009 Tomas Doran. This code is licensed on the same terms as perl
itself.

=cut

