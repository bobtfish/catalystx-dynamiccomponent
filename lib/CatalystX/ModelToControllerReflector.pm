package CatalystX::ModelToControllerReflector;
use Moose::Role;
use namespace::clean -except => 'meta';

with 'CatalystX::DynamicComponent' 
    => { alias => { _setup_dynamic_component => '_setup_dynamic_controller' } };

requires 'setup_components';

after 'setup_components' => sub { shift->_setup_dynamic_controllers(@_); };

sub _setup_dynamic_controllers {
    my ($app) = @_;
    my @model_names = grep { /::Model::/ } keys %{ $app->components };
    
    foreach my $model_name (@model_names) {
        $app->_reflect_model_to_controller( $model_name, $app->components->{$model_name} );
    }
}

sub _reflect_model_to_controller {
    my ( $app, $model_name, $model ) = @_;

    my $class = blessed($app) || $app;

    my $controller_name = $model_name;
    $controller_name =~ s/::Model::/::Controller::/;

    my $suffix = $model_name;
    $suffix =~ s/^.*::Model:://;

    my $controller = $app->_setup_dynamic_controller( $controller_name, {}, sub {
        shift->next::method(@_); # Just use the default COMPONENT method
    });
    my $meta = $controller->meta;
    $meta->make_mutable; # Dirty, I should build the class, add the methods, then
                         # last of all make it a component
    $meta->remove_method('COMPONENT');
    $meta->superclasses($app . '::ControllerBase');

    my $methods = $model->meta->get_method_map;
    foreach my $method_name (keys %$methods) {
        $controller->meta->add_method(
            # Note need to pass model name, as the method actually comes from
            # the underlying model class, not the Catalyst shim class we autogenerated.
            $method_name => $app->generate_reflected_controller_action_method($suffix, $methods->{$method_name})    
        );
    }
    $meta->make_immutable;
}

sub generate_reflected_controller_action_method {
    my ( $app, $model, $method ) = @_;
    my $method_name = $method->name; # Is it worth passing the actual method object here?
    sub {
        my ($self, $c, @args) = @_;
        $c->res->header('X-From-Model', $model);
        $c->res->header('X-From-Model-Data', $c->model($model)->$method_name(@args));
        $c->res->body('OK');
    };
}

1;

